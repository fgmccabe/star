= Making choices

One of the common themes of systems that `face the world' (aka online
applications) is that they often have to make decisions. More
specifically, any outward facing system will receive requests that the
system must decide whether they are legitimate or not. Before acting
on a request, we must decide whether or not to trust it.




events, a _Sink_ which represents externally visible actions being
taken by the system, and, finally, a _Store_.

We are being deliberately simplistic here; because we wish to focus on
a part of this picture: the _Store_.

A _Store_ is part of most online applications because applications
need some form of context: there is typically not enough information
in an event to determine how to react to it: we must also have some
history as well. For example, if an event represents a log-in attempt,
then in order to know how to respond, we need to know if the
originator has an account, whether the offered password is consistent
with our records and so on.

The _Store_ concept is simply an abstract way of representing this
context. However, we can go a little further and claim that the
dominant mode of accessing a store is through _queries_.

A query is simply an expression that is evaluated in the right
context; however, the natural semantics for query processing is more
like that of database searches than arithmetic evaluation. The reason
for that is that a typical _Store_ may be quite large and serve
multiple functions. As a result, Star queries have a lot in common
with logic and database programming paradigms.

== Queries

Consider, if you will, the problem of finding a set of
grandparent-grandchild pairs -- given information about parent-child
relationships. For example, suppose that we had a list of pairs - each
pair indicating a parent and child:
[source,star]
----
parents:cons[(string,string)].
parents = [("john","peter), ("peter","jane"), ... ].
----
and that we wanted to construct a result list -- also of pairs --
along the lines of:
[source,star]
----
GC:cons[(string,string)].
GC = [("john","jane"),...].
----
Computing the list of grandparent/grandchildren pairs involves
searching the ``parents`` for pairs that satisfy the grandparent
relationship. This, in turn, involves a double iteration: each pair in
the ``parent``s list might represent the upper or lower half of a
grandparent/grandchild relationship (or both).

Based on the collection operators we have seen so far, we can build
such a search using two ``foldLeft`` operations:
[source,star]
----
foldLeft(
 (SoFar,(X,Z)) => foldLeft(
   let {
     acc((ZZ,Y),gp1) where Z==ZZ => [gp1..,(X,Y)].
     acc(_,gp1) => gp1.
   } in acc,
   SoFar,parents),
   list of [],
  parents)
----
This, rather intimidating,footnote:[There are, unfortunately, some
functional programmers that revel in complex code expressions like
this one. We are not one of them!] expression uses one ``foldLeft``
to look for the grandparent, and the second ``foldLeft`` finds all
the grand-children. All without any explicit recursion.

The ``acc`` function defined above in the ``let`` expression
implements the logic of deciding what to accumulate depending on
whether we had found a grandparent or not.

The various filter, ``fmap`` and ``foldLeft`` functions _are_
powerful ways of processing entire collections. However, as we can
see, combination iterations can be difficult to construct and harder
to follow; something that is not helped by the occasional need to
construct complex functions in the middle, as in this case.

What is needed is a way of expressing such complex query conditions in
a way that can be _implemented_ using ``foldLeft`` expressions
but which are easier to read.

Consider the conjunction:
[source,star]
----
(X,Z) in parents && (Z,Y) in parents
----
This could be read as specifying what it means to be grandparents:

[aside]
For which ``X``, ``Y`` and ``Z`` can we assert that ``X``
is the parent of ``Z``, and ``Z`` is the parent of ``Y``?.

This should be significantly easier to follow than the complex
expression above; and it is easier to be sure whether it is a correct
representation of what it means to be a grand parent.

=== Satisfaction semantics

Conditions like the ones above are boolean valued -- but they are not
always expressions. For example, the first condition there being a
parent:
[source,star]
----
(X,Z) in parents
----
is not evaluated in the way that expressions are normally evaluated --
by _testing_ to see if a given pair of ``X`` and ``Z`` are
in some ``parents`` collection. Instead, the condition needs to be
evaluated by trying to _find_ ``X`` and ``Z`` that are in
the ``parents`` collection. Only if, and when, suitable bindings
for ``X`` and ``Z`` are found does the condition `return' true;
otherwise it returns false. In effect, the condition becomes a
_search_ for suitable candidate pairs.

Technically this is called _satisfying_ the condition -- to
distinguish what is going on in normal expression
_evaluation_. Of course, satisfying and evaluating are close
cousins of each other and amount to the same thing when there is no
search involved.

In addition to individual _search conditions_ like this, it is
also possible to use logical operators -- called _connectives_ --
to combine conditions. In the case of our grand-parent query, there is
a conjunction; which uses the variable ``Z`` to acts as a kind of
glue to the two search conditions.

[aside]
In database query terminology, conjunctions like this one amount to
_inner joins_. Languages like SQL have many operators and
features to make expressing queries easier; the fundamental semantics
of Star's queries are similar in power to those of SQL.

The available connectives include the usual favorites: conjunction
(``&&``), disjunction (``||``), and negation (``~``). We may
also see the need for some less familiar connectives: ``implies``
and ``otherwise``.

[aside]
****
An ``implies`` connective is a way of testing complete compliance
with a condition; for example, we can define a query capturing the
situation that a manager earns more than his/her members by requiring
that anyone who works for the manager earns less than they do:
[source,star]
----
(X,M) in worksFor *> X.salary=<M.salary.
----
****

=== Abstracting queries

The grandparent condition shows how to define what grandparent-hood
means; but we also need ways of abstracting and naming such
queries. The most straightforward way of this is to use a _query
abstraction_ expression. For example, we can embody the grandparent
situation in:
[source,star]
----
{ (X,Y) | (X,Z) in parents && (Z,Y) in parents}
----
The value of a query abstraction expression is typically a ``set``
-- in this case a set of the pairs of strings in the
grandparent-grandchild relation.

Since it's an expression, we can assign it to a variable:
[source,star]
----
gp = { (X,Y) | (X,Z) in parents and (Z,Y) in parents}
----
and we can feed this set into another query:
[source,star]
----
(X,"f") in gp
----
and we can define functions whose values are the results of queries:
[source,star]
----
gpOf(GC) => {X | (X,GC) in gp}
----
We shall explore query expressions a little more, but first an
editorial:

=== A Splash of Inference

Queries have an important role in Star for two reasons: queries
often have a much more obvious and transparent semantics than other
programs -- even functional programs! Secondly, queries have a deep
connection to logic.

There are many reasons why one might be interested in logic; from a
theoretical modeling perspective (does the universe follow rational
rulesfootnote:[Surprisingly, yes! Of course, discovering the
rationality may be hard; but the immense success of Western thought
was only possible because the universe is very rationally
constructed.]) to the deeply pragmatic reason that logical programs
are often easier to understand and therefore easier to trust.

Like programming languages, it turns out that there are many kinds of
logic. Again, like programming languages, there is a trade-off between
expressivity and complexity. The primary source of complexity in a
rule language is the machinery needed to realize it -- together with
understanding it sufficiently to be able to predict the meaning of a
written rule.

A somewhat simplified enumeration of the different kinds of logic might be:

Propositional calculus:: This is characterized by single-letter conditions
(sometimes confusingly called _predicate variables_) and a guaranteed finite
evaluation mechanism.
Datalog:: This is characterized by relations with simple unstructured values
(i.e., strings and numbers). Execution in Datalog has similar performance
characteristics as querying databases.
First Order Predicate Calculus:: This is probably the most well known and well
understood logic. From an expressiveness point of view its focus is on the
logical relationships amongst individual entities -- which includes things like
trees, lists and so on. Inference in First Order has many of the same
characteristics as program evaluation: not decidable in general but many
effective sub-cases.
Higher Order Predicate Calculus:: There are actually many higher-order
logics. The main expressive enhancement over First Order is that one can
directly talk about relationships between entities as well as entities
themselves. The cost of this is that inference becomes problematic -- even
equality is undecidable.

Each of these levels represents a step both in expressiveness and in
complexity. In general, the right logic for your application is
something only you can decide; however, in designing a language, we
have to choose for you.

In our view, there is a sweet spot between Datalog and First Order
Logic. Datalog allows one to right rules (unlike pure SQL) but is not
capable of handling arbitrary data structures. On the other hand, it
may be that _recursion_ is something that we can do without -- as
we have seen earlier, many well structured functional programs have no
explicit recursion.

However, we must also be able to _embed_ our logic into our more
regular programs. The key goal here is to maximize the benefit of
providing a logical formalism whilst minimizing the burden on both the
programmer and on the language implementation. This also recognizes
that, while important, logical reasoning is typically only a small
part of an overall system. It also recognizes the fact that gaps in
the reasoning capability of a system can be patched more easily if the
logic is simpler.

And so, in Star, we highlight the _query_ aspect of logical
reasoning and bless queries as first class entities in the language.

Critically, queries have a _declarative_ semantics as well as a
_programmatic_ one; this dual reading is essential if one is to
be able to understand the reasoning.

.Historical note
[aside]
in earlier iterations of the design for embedding logic into Star, a
more-or-less complete rule system was envisaged. Such inference rules
would have a similar status to functions and equations do. However, a
combination of complexities and edge cases (such as how to handle a
combination of inputs and outputs in rules) lead the designers to
radically simplify the proposal and simply focus on queries. This gave
us 90% of the potential benefit of inference rules at 10% of the cost.

Sometimes, a splash of logic is all we need. In terms of styles of
logic, our approach is most reminiscent of _answer set
programming_.

=== Anatomy of a Query

A query can be seen as combining two elements: a _condition_ and
an _answer template_. A query condition may be _satisfied_
in one or more ways -- each time potentially binding variables in the
condition to values -- and the answer template encodes how we want to
use the result of a successful satisfaction. Notice that the variables
that are bound by the condition _are in scope_ within the answer
template.

The syntax and style of Star’s query notation has strong echoes
with SQL’s syntax -- deliberately so. Specifically, we take SQL’s
_relational calculus_ subset -- the language of wheres and of
boolean combinations. Star’s query expressions do not have the
equivalent of explicit relational join operators.


==== Query Conditions

The condition takes the form of a boolean combination of
_predications_; a predication is either a normal boolean-valued
expression, a _match_ condition, or a _search_
condition. Various types of boolean combinations are supported; the
most common being conjunction (``&&``), disjunction (``||``) and
negation (``~``).

We have already seen match conditions; for example:
[source,star]
----
some(X) .= opValue
----
is a match condition that is satisfied if the value of ``opValue``
matches the pattern ``some(X)``. A successful match has the
additional effect of binding ``X`` to the value embedded in the
``some`` value.footnote:[The form ``X?=opValue`` is actually a
short form of the same condition.]

Where a match condition has at most one way of being satisfied, a
search condition can potentially have many solutions. Search
conditions look like:

[source,star,subs="quotes"]
----
_Pattern_ in _Expression_
----

We saw an example of this earlier in our grandparent query:
[source,star]
----
(X,Z) in parents
----
As should be anticipated at this point, search is realized via a contract. This
allows us to search any type -- so long as the ``iterable`` contract is
implemented for that type.

Maps also have their own special variant of a _query search
condition_. A condition of the form
[source,star]
----
K->V in D
----
where D is a ``map`` will be satisfied if there is a key/value pair
in D corresponding to K and V. For example, the condition:
[source,star]
----
K->V in map of [1->"alpha", 2->"beta"] && V=="alpha"
----
is satisfied for only one pair of ``K`` and ``V``: namely
``1`` and ``"alpha"`` respectively.

The final form of query condition is simply the boolean-valued expression. Note
that, unlike the other forms of query condition, boolean expressions are
test-only: they cannot result in bindings for query variables.

==== The ``iterable`` contract

The ``iterable`` contract is similar in intention to the
``folding`` contract we saw before. However, it is more tuned to
supporting different combination of queries.

The ``iterable`` contract looks like:
[source,star]
----
contract all s,e ~~ iterable[s->>e] ::= {
  _iterate:all r ~~ (s,(e,iterState[r])=>iterState[r],iterState[r]) => iterState[r].
}
----
This contract states that to search a collection, you have to be able
to iterate over it using the ``_iterate`` function. The
``_iterate`` function uses the ``iterState`` type to help guide
the search:
[source,star]
----
all t ~~ iterState[t] ::= .noneFound      -- no results yet
                      | .noMore(t)        -- all needed results found
                      | .continueWith(t)  -- keep going
                      | .abortIter(string).
----
The different cases in the ``iterState`` type codify different
things that can happen during a search. The most commonly used case is
the ``continueWith`` case -- which is used to encapsulate the
results found so far.

There are two companion contracts to the ``iterable`` contract: the
``indexed_iterable`` contract supports search over key/value pairs
and the ``generator`` contract is used to help construct answers.

==== Answer Templates

The result of a query is governed by the _answer template_ of the
query abstraction. There are two main forms of answer template: the
expression template and the fold template.

Expression Template:: A _expression template_ is simply an expression. 

The expression template is evaluated for each successful way that the
query condition can be satisfied; typically, there are free variables
in the template expression that refer to variables bound in the query
condition. That way. values found during the search can be extracted
and made part of the overall answer.

Fold Template:: A fold template is used when it is desired to aggregate over the found
solutions. The form of a fold template is:

[source,star]
----
fold Exp with Fn
---- 

We saw this form of query at the beginning of the book where we looked
at nice ways of adding up elements of a list:

[source,star]
----
{ fold X with (+) | X in L }
----

Bounded Query:: A bounded query is similar to the regular query except that we
restrict the search to a fixed number of answers.

The form of a bounded query template is:

[source,star]
----
N of Exp 
----
For example, since we know that a person can have at most 2 parents
(special gene therapy excepted), we can ask for someone's parents
using:

[source,star]
----
parentsOf(X) => { 2 of P | (P,X) in parents }
----

== Classifying

Recall that we asserted that classification is a key part of any
outward facing system: the system has to decide how to act on an
incoming request. It may be instructive to see just how the query
formalism can be used to help.

Let us imagine a system that has some external face, for example, a
server that allows users to fetch and store documents. We have a
business requirement to allow anyone to store and fetch documents;
however, we also reserve the right to not process bad
documents.footnote:[However that is defined!]

Since we anticipate enormous success for our service, and we also
anticipate that some people will try to game our system in order to
further their own nefarious goals. So, we have to put in place a
classification system that can decide how to process requests and
which we anticipate will need continuous evolution.

We have already claimed that the simplest online processing system can
be viewed as having four components: a source of events, a processor,
a sink where the output of the system is targeted and a knowledge base
that is used to inform the processor.

.A Simple Event Processor
image::online.png[]

One of the tasks that the processor must perform is to decide how to
react to incoming events; this is an example of a _classification
problem_; in this case we are classifying events into those we choose
to act on and those we will ignore (or complain about).

Classification always depends on a combination of information gleaned
from the event itself and on global or contextual information. In our
case, we can usefully breakdown the contextual knowledge into two
kinds: _rules_ that embody our processing policy and
_reputation_ data that the system has collected about external
entities.

.Rules and Reputations
image::classifier.png[]

We distinguish these two sources of knowledge, partly because the way
that we collect and use them are different; but mainly because they
are about different things. However, we can use our query formalisms --
together with functions -- for both.




