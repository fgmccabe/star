= JSON

(((json, Using the json type)))
The JSON Infoset type, or just `json` type, allows values to be represented in a
way that is easily digestible by many web-based tools -- including browsers. The
`json` type is semantically equivalent to the JSON structure defined in
<https://www.ietf.org/rfc/rfc4627.txt>. 

In addition to basic handling of JSON values, Star provides a form of path
notation that allows `json` values to be probed and updated.

NOTE: In order to use json in your program you need to import the `star.json` package:

[source,star]
----
import star.json.
----

[#jsonType]
== The `json` Type

(((json type)))
The `json` type is defined as:

.The `json` type
[source,star]
----
  public json ::=
      .jTrue
    | .jFalse
    | .jNull
    | .jTxt(string)
    | .jColl(map[string,json])
    | .jSeq(cons[json])
    | .jNum(float).
----

For example, the JSON value:
[source,json]
----
{
  "Image": {
    "Width":  800,
    "Height": 600,
    "Title":  "View from 15th Floor",
    "Thumbnail": {
      "Url":    "http://www.example.com/image/481989943",
      "Height": 125,
      "Width":  "100"
    },
    "IDs": [116, 943, 234, 38793]
  }
}
----
can be represented using the `json` value:

[#infosetEx]
.An Example `json` Value
[source,star]
----
.jColl({
  "Image" -> .jColl({
    "Width" -> .jNum(800.0),
    "Height" -> .jNum(600.0),
    "Title" -> .jTxt("View from 15th Floor"),
    "Thumbnail" -> .jColl({
      "Url" -> .jTxt("http://www.example.com/image/481989943"),
      "Height" -> .jNum(125.0),
      "Width" -> .jTxt("100")
    }),
    "IDs" -> .jSeq([
      .jNum(116.0), .jNum(943.0), .jNum(234.0), .jNum(38793.0)
    ])
  })
})
----

The JSON standard specification is mute on the topic of numeric precision. We
choose to represent numeric values as `float`.

[#infoPath]
== Infoset paths

(((path access to json)))
(((json,path access)))
Infoset values are typically deeply nested structures involving both accessing
dictionary-like collections and arrays. In order to make working with `json`
values simpler we introduce the concept of an json path -- an `infoPath`.

An `infoPath` is a list of `jsonKey` elements -- each of which represents either
an index into a `.jSeq` sequence of elements or the name of a member of a
`.jColl` collection of elements. This is captured in the definition of the
`infoPath`, as defined in:

.The `jsonKey` type
[source,star]
----
public infoPath ~> cons[jsonKey].

public jsonKey ::= .jField(string) | .jIndex(integer).
----

For example, the path expression that denotes the url of the thumbnail in
<<infosetEx>> is:

[source,star]
----
[.jField("Image"),.jField("Thumbnail"),.jField("Url")]
----

and the path that denotes the first id from the `IDs` sequence is:
[source,star]
----
[.jField("Image"),.jField("IDs"),.jIndex(0)]
----

Infoset paths are used in several of the functions that are defined on `json`
values.

== Standard Functions on Json Values

Several contracts are implemented for `json` values; including `indexed`,
`iter`, `display` and `coercion` (to and from `string` values).

=== `_index` access to `json`

(((json,_index)))
(((_index,json)))
The `_index` function applies an `infoPath` to an `json` to obtain a portion of the `json` value. It's type is:
[source,star]
----
_index:(infoPath)=>option[json].
----

`_index` is part of the `indexed` contract -- see <<indexedContract>>.

(((json,standard functions)))
(((_put,json)))

=== `_put` -- Set a Value in an `json`

The `_put` function updates a value in an `json` -- depending on a path -- and
returns the updated `json`.

[source,star]
----
_put:(json,infoPath,json)=>json.
----

NOTE: This function does not update the original; it returns a new value.

To use this function to change the title of the value in <<infosetEx>> one might
use the expression:

[source,star]
----
I[[.jField("Image"), .jField("Title")]->.jTxt("A Better One")]
----

NOTE: If the path prescribed by the `infoPath` expression does not go through
`.jColl` and `.jSeq` elements of the `json` structure, those elements will be
overwritten with the new value.

(((json,_remove)))
(((_remove,json)))
=== `_remove` -- Remove a Value from an `json`

The `_remove` function removes a value in an `json` -- depending on a path --
and returns the modified `json`.

NOTE: This function does not update the original; it returns a new value.

The type of `_remove` for `json` values is given by:
[source,star]
----
_remove:(json,infoPath)=>json
----

(((json,_iter)))
(((_iter,json)))

=== `_iter` -- Iterate Over an `json`

The `_iter` function is used when iterating over the elements of an `json`. 

The type of `_iter` is given by:
[source,star]
----
_iter:all x ~~ (json,x,(json,x)=>x)=>x
----

The `_iter` function is part of the `iter` contract -- <<iterContract>>.

The `json` variant of the `_iter` function calls the `client function' for all
of the `leaf' elements of an `json` value. For example, in the query condition:

[source,star]
----
X in I
----

where `I` is the `json` value shown in <<infosetEx>>, then the client function
will be called successively on the `json` values:

[source,star]
----
.jTxt("100")
.jNum(125.0)
.jTxt("http://www.example.com/image/481989943")
.jNum(115.9999999899999)
.jNum(942.9999999899996)
.jNum(233.9999999899999)
.jNum(38793.0)
.jNum(800.0)
.jNum(600.0)
.Txt("View from 15th Floor")
----

== Parsing and Displaying

The standard contract for displaying values --
`display` -- is implemented for the `json` type. In addition, a `string` value
may be parsed as a `json` by using the coercion expression:

[source,star]
----
"{\"Id\" : 34 }" :: json
----

has value:
[source,star]
----
.jColl({ "Id" -> .jNum(34.0) })
----

