= Abstract Grammar

(((grammar)))
(((abstract syntax tree)))
The grammar of Star is based on an _operator precedence
grammar_. Commonly, operator precedence grammars are restricted to the
arithmetic expression subset of a language; we use it for the whole of
Star. This allows us to address the full grammar and semantics of Star
in a layered approach. The grammar layer is focused on the _well
formed formedness_ of Star programs -- which expressions are
syntactically valid even though they may not be type correct.

In effect, the grammar layer represents the second layer (above the
lexical layer) in the 'tower of semantics' that represents the full
semantics of Star programs.

TIP: This choice gives us two key benefits: it is simple to understand and
it is simple to extend. The latter is particularly important in
Star as a significant part of its functionality is derived from
_profiles_ which are similar to macros.

However, it also makes certain other aspects more challenging. In
particular, an operator precedence grammar knows less about the
program as it is parsed. This means that syntax error messages may
sometimes be less informative.

== Operator Precedence Grammar

(((operator precedence grammar)))
An operator precedence grammar (OPG) allows us to write expressions like:
[source,star]
----
X * Y + X / Y
----

and to know that this means the equivalent of:
[source,star]
----
(X * Y) + (X / Y)
----

or more specifically:
----
+(*(X, Y), /(X, Y))
----

Operator precedence grammars are often used to capture
arithmetic-style expressions. In Star we extend the concept to cover
the entire language.

For example, an equation such as:
[source,star]
----
double(X) => X*X
----

can be interpreted -- by treating `=>` as an operator -- as:
[source,star]
----
'=>'(double(X),'*'(X,X))
----

Of course, this is merely a _parse_ of the equation. The real
task of a compiler is to interpret this abstract syntax as an
equation rather than as an attempt to apply the `=>`
function.

[#srOpPrecGrammar]
.Operator Precedence Grammar
[source,star]
----
term(Priority) >> T --> prefix(Priority) >> T.
term(Priority) >> T --> infix(Priority) >> T.
term(Priority) >> T --> postfix(Priority) >> T.
term(Priority) >> T --> term0 >> T.

prefix(Priority) >> Op(A) --> [Op],
   { (Pr,RPr) ?= isPrefixOp(Op) && Pr =< Priority },
   term(RPr) >> A.

infix(Priority) >> Op(A,B) --> term(LPr) >> A, [Op],
   { (LPr,Pr,RPr) ?= isInfixOp(Op) && Pr =< Priority },
   term(RPr) >> B.

postfix(Priority) >> Op(A) --> term(LPr) >> A, [Op],
   { (LPr,Pr) ?= isPostfix(Op) && Pr =< Priority }.

term0 >> T --> literal >> T.
----

Note that the grammar in <<srOpPrecGrammar>> is based on Star's
grammar notation (see <<Grammar Rules>>) but is presented here for
illustative purposes: it is not an executable grammar.

== Standard Operators

A key input to the grammar is the table of operators. There are a
large number of standard operators; some of which are symbolic and
others are alphanumeric in form.

The standard operators are listed in <<standardOperatorsTbl>>. Operators in
this table are listed in order of priority. Together with a priority,
operators can also be considered to `prefix`, `infix`,
`postfix`, or some combination of the three.

The priority of an operator is the indication of the `importance' of
the operator: the higher the priority the nearer the top of the
abstract syntax tree the corresponding structure will be.

Priorities are numbers in the range 1..2000; by convention, priorities
in the range 1..899 refer to entities that normally take the role of
expressions, priorities in the range 900..1000 refer to predicates and
predicate-level connectives and priorities in the range 1001..2000
refer to entries that have a statement or program level
interpretation.

include::operators.adoc[]

== The `quote` contract
[#quoteContract]

The `quote` contract, together with the `<||>` brackets, allows Star programs
some mediated access to the underlying operator precedence grammar.

The contract -- as defined in <<quoteContractProg>> -- has elements that
correspond to major abstract syntax features.

[#quoteContractProg]
.The `quote` contract
[source,star]
----
public contract all a ~~ quote[a] ::= {
  _name : (option[locn],string) => a.
  _qnme : (option[locn],string) => a.
  _integer : (option[locn],integer) => a.
  _biginteger : (option[locn],bigint) => a.
  _float : (option[locn],float) => a.
  _char : (option[locn],char) => a.
  _string : (option[locn],string) => a.
  _tuple : (option[locn],string,cons[a]) => a.
  _apply : (option[locn],a,a) => a.
}
----

Here, `locn` is a standard type that denotes a source location.  Of course, this
is just a contract; to be useful, it has to be implemented. The Star compiler
has its own implementation, but user implementations of the `quote` contract are
certainly possible.

The power of this contract becomes evident when combined with the `<||>` quote
syntax: a Star fragment enclosed in `<|` ... `|>` quotes is translated into
calls of the functions in the `quote` contract:

[source,star]
----
foo() => <|A+B|>.
----

is equivalent to:
[source,star,subs="quotes"]
----
foo() => _apply(_loc~1~_,_name(_loc~2~_,"+"),_tuple(_loc~3~_,"()",[_name(_loc~4~_,"A"),_name(_loc~5~_,"B")])).
----
where `_loc~i~_` are locations within the `foo` function's source.

NOTE: There is also the equivalent of an anti-quote for the quote syntax. The
`$` operator used within a quoted form acts as an escape:

[source,star]
----
foo(X) => <|A+$X|>.
----

is equivalent to:

[source,star,subs="quotes"]
----
foo(X) => _apply(_loc~1~_,_name(_loc~2~_,"+"),_tuple(_loc~3~_,"()",[_name(_loc~4~_,"A"),X])).
----

Of course, the variable `X` here must have the correct type -- which depends on
the implementation of the `quote` contract that is in force.




