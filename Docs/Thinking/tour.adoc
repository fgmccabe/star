= A Tour of Star

== Texture

All programming languages can be said to have a particular style or
texture. This is often so strong that it only needs a single line of
source code to be able to identify a programming language. In the case
of Star, this line might be (we will explain what it means a bit later on):

[source, star]
----
factorial:all t ~~ arith[t] |= (t)=>t.
----

This is a _type annotation_: a description of the type of a
variable -- in this case `factorial`. It is a function type --
which means that we can call `factorial`. It is also, somewhat
unusually for factorial, _generic_: the factorial of any type can
be computed -- provided that `arith`metic has been implemented for it.

Although this is not the simplest type annotation; overall, the
texture of Star is designed to enhance the readability of
programs. Readability is important because many people have to be able
to read programs; perhaps more than are involved in writing them. Even
with an emphasis on readability, the power of the language is such
that Star programs tend to be shorter than their equivalents written
in languages like Java.

This is a function definition that should be more straightforward to
understand:

[source, star]
----
doubleMe:(integer)=>integer.
doubleMe(x) => x + x.
----

Although mostly not mandatory, in most cases we strongly favor a
programming style where the type of functions and other top-level
definitions are declared explicitly. Here is another function
calling the just-defined `doubleMe`:

[source, star]
----
doubleUs:(integer,integer)=>integer.
doubleUs(x, y) => doubleMe(x) + doubleMe(y).
----

A complete Star program that uses the `doubleMe` and
`doubleUs` functions to print a result is:

[source, star]
----
sample.first{
  import star. -- Standard types and functions
  
  doubleMe:(integer)=>integer.
  doubleMe(x) => x + x.

  doubleUs:(integer,integer)=>integer.
  doubleUs(x, y) => doubleMe(x) + doubleMe(y).

  main:()=>().
  main() => valof {
    show doubleUs(3,4);
    valis ()
  }
}
----

== Equations as Cases

Many functional programming languages — including Star — are based on
an equational style. Equational programs are written as a series of
equations; for example, in the credit function:

[,star]
----
credit:(customer)=>float.
credit(C) where inGoodStanding(C) => 0.15.
credit(C) where inDefault(C) => -0.1.
  ...
credit(_) default => 0.0.
----

Each line of this definition is an equation. It specifies the meaning
of the credit function in a restricted setting – the complete function
is understood by combining the cases together.

Using equations we can separate out the different cases for the
customer's credit and '`work on them separately`'. This allows the
programmer to focus attention and allows for rapid editing and
reorganization should the need arise.  Case-based approaches to
programming are significantly more productive than traditional
if-then-else procedural programming.

=== Patterns

Patterns and pattern matching are ubiquitous in Star. Nearly all
literal forms have a pattern equivalent that allows matching and, most
importantly, de-structuring such values.

[source, star]
----
lucky:(integer)=>string.
lucky(7) => "LUCKY NUMBER SEVEN!".
lucky(x) default => "Sorry, you’re out of luck, pal!"

fctrl:(integer)=>integer.
fctrl(0) => 1.
fctrl(n) where n > 0 => n * fctrl(n-1)
----

In fact, this last function is not fully paradigmatic of Star. A
better, albeit more complex, version of this would be:

[source, star]
----
factorial:all t ~~ arith[t] |= (t)=>t.
factorial(zero) => one.
factorial(n) where n > zero => n * factorial(n-one)
----

This version of factorial will work with integers, floats anything
that can do arithmetic. Here we use special variables `zero` and
`one` to denote 0 and 1 in a type-agnostic way.

== Higher Order Functions

Star supports higher-order functions. The following `apply` function
corresponds to the familiar map function from other functional
languages.

[source, star]
----
apply:all s,t ~~ ((s)=>t,cons[s])=>cons[t].
apply(f, .nil) => .nil.
apply(f, .cons(x, xs)) =>
      .cons(f(x), apply(f, xs)).
----

Although Star has a sophisticated type system that supports automatic
type inference, we recommend that that programmers supply type
annotations, which often improve readability.

[NOTE]
====
Specifically, the rule for type inference is that Star will
automatically infer the types of all variables; but will never infer a
_generic_ -- or quantified -- type for functions.

This represents an interesting balance between convenience -- many
programs are not actually fully generic -- and precision.
====

So, since `apply` is supposed to be generic, we are required to
supply a type annotation for it:

[source, star]
----
apply:all s,t ~~ ((s)=>t,cons[s])=>cons[t].
----

This states that `apply` is generic in two types, that it takes a
function argument and a `cons` list argument and returns a new
`cons` list.


For example, we can define functions over collections that allow us to
(say) compute the maximum element in a collection:

[source, star]
----
maximum:all t ~~ arith[t] |= (list[t],t)=>t.
maximum([H,..T]) => let {
  max([],Mx) => Mx.
  max([F,..R],Mx) where F>Mx => max(R,F).
  max([_,..R],Mx) default => max(R,Mx).
} in max(T,H)
----

The `max` function enclosed in the `let` body uses patterns to
match against the successive elements of the collection.

=== Sequence Patterns

The pattern form:

[source, star]
----
[H,..T]
----

matches any non-empty sequence; and binds the first element of the
sequence to the variable `H` and the remainder of the sequence to
`T`.

The pattern form:

[source, star]
----
[]
----

only matches an empty collection.

The `maximum` function is written in terms of a local function –
`max` – and a call to that local function. The `let`
expression is one of the foundations and cornerstones of functional
programming.

[NOTE]
====

Notice that the type annotation for `maximum` has a special
`arith` clause attached to it:

[source, star]
----
maximum:all t ~~ arith[t] |= (cons[t],t)=>t.
----

The clause `arith[t] |=` means that the type variable `t` is
_constrained_ -- it must be a type that _implements_ the
arithmetic contract -- `arith`.

Type contracts like this one are fairly ubiquitous in Star; they
represent similar functionality to type classes in Haskell or traits
in some modern language like Rust or Go.
====

== Defining Types

Like most functional languages, Star supports many forms of sequence
collection natively. One of the more common kinds of list is the ‘cons
list’ a.k.a. single-linked list. Cons lists are defined using a type
definition that introduces the type and its different ‘shapes’:

[source, star]
----
all t ~~ cons[t] ::= .nil | .cons(t,cons[t]).
----

This states that there are two forms of `cons` lists: the empty cons
list `.nil`, and a pairing `.cons(x, xs)` where `x` is
the first element, and `xs` is the rest of the list. The
`all` quantifier at the beginning of the declaration of the `cons`
type marks the type as a generic type – specifically a universal type.

NOTE:
Notice the period in front of the `.nil` enumeration symbol and
the `.cons` symbo. This allows us (the compiler and the
programmer) to be more obvious in distinguishing such symbols from
variables. There are other benefits, but clarity is the main
driver.footnote:[It _does_ have the somewhat unfortunate
side-effect of requiring a period in front of boolean `.true` and
`.false` literals.]

Nearly all Star values can be written as literals, including user defined
types like cons. For example, the `cons` list:

[source, star]
----
.cons("alpha",.cons("beta",.cons("gamma",.nil)))
----

contains the strings `"alpha"`, `"beta"` and `"gamma"`.

The standard implementation of `cons` lists in Star also supports
the sequence notation we saw above. So, this list can also be written:
[source, star]
----
["alpha", "beta", "gamma"]:cons[string]
----

NOTE:
We are not allowed to mix strings and integers in the same
list. That is because Star is strongly, statically typed and requires
all elements of a list to have the same type.

Just as with sequences, we can define functions over `cons` lists via
pattern matching:

[source, star]
----
maxCons:(cons[integer]) => integer.
maxCons(.cons(x, .nil)) => x.
maxCons(.cons(x, xs)) default => let {
     maxRest = maxCons(xs).
   } in (x > maxRest ?? x || maxRest)
----
   
The first `maxCons` equation is for `cons` lists with one
element, where `maxCons` simply returns that element. The second
equation first computes the maximum of the rest of the list via a
recursive call, and uses a conditional expression to compute the
maximum for the entire list.

The expression

[source, star]
----
(x > maxRest ?? x || maxRest)
----

is a conditional expression: if `x` is greater than
`maxRest` then it is the maximum; otherwise `maxRest`
is. This is an example of a very well known form of expression that
uses symbolic operators rather than keywords.

== Collections

Star has a standard way of writing sequences and collections – like
lists and dictionaries. For example, the expression

[source, star]
----
[4, 8, 15, 16, 42]
----

denotes the list with the numbers 4, 8, 15, 16 and 42 in it. This
notation – which is called the sequence notation.  We can denote a
different kind of sequences, containing cons elements for example,
using:

[source, star]
----
[4, 8, 15, 16, 42]:cons[integer]
----

or, we can even denote a general collection without being explicit
about the type of collection:

[source, star]
----
[4, 8, 15, 16, 42]
----

In this last case, the compiler may have to rely on the context to
fully determine the type of the sequence.

As another example, we can see that a dictionary literal is also a
variation on the collection – except that we have keys as well as
values:

[source, star]
----
{ "alpha"->1, "beta"->2 }
----

=== Comprehensions

Star has a built-in query expression notation that allows us to avoid
a lot of common recursive forms of definition. For example, the
`apply` function can be written more succinctly using:

[source, star]
----
apply(f,C) => { f(X) | X in C }
----

the meaning of the expression is equivalent to the recursive
definition we saw of `apply` above.

== Contracts

Star contracts are analogous to type interfaces: they specify the
names and types of a collection of definitions.  For example, the
contract for basic arithmetic is

[source,star]
----
contract all t ~~ arith[t] ::= {
    (+) : (t,t)=>t.
    (-) : (t,t)=>t.
    (*) : (t,t)=>t.
    (/) : (t,t)=>t.
    one : t.
    zero : t.
}
----

This has the effect of declaring a group of functions that work
together to form a basic arithmetic suite.

Once defined, a contract can be used simply by mentioning one of its
members. Thus, in this `fact` function, the use of the `*` and `-`
operators means that `fact` depends on the `arith` contract:

[source,star]
----
fact(0) => 1.
fact(X) => X*fact(X-1)
----

NOTE: Although we use `*` and `-` using a normal infix notation,
semantically, these are just regular function identifiers. The
contract specifies the types of these operators by enclosing them in
parentheses -- this is just to inform the compiler that the occurrence
is as an identifier and not as an operator.

If we want to state that the type of a variable is
`something' that implements a contract we can do so by adding a
constraint to the variable's type:

[source,star]
----
F : arith[t] |= t
----

This has the effect to declaring that `F`'s type is a type variable —
but one that is constrained such that whatever concrete type it
eventually gets, it must be one that is defined for `arith`.

In fact, however, this version of factorial is not the most
paradigmatic (sic) version. The reason is that the type signature for
`fact` is not generic but tied to integer:

[source,star]
----
fact:(integer)=>integer
----

The reason that `fact` is not fully generic is interesting: it’s the
presence of the literal numbers 0 and 1 in its definition. The type of
any literal integer is integer! This is enough to force the type of
fact to be restricted to integers.  In particular, Star does not
support implicit coercion, including coercion of literal numbers from
integer to any other type.

The solution is simple, if a little strange: we declared in the
`arith` contract two non-function entries: `zero` and `one`.  By
explicitly identifying zero and one as elements of the arith contract
we can use them in lieu of 0 and 1 respectively without committing to
integers.footnote:[There is some limited Mathematical justification
for this: the `arith` contract defines a _Field_ which requires zero
elements for the two operators (`+` and `*`).]

Using `one` and `zero`, we can write a much more paradigmatic version of the
factorial function:

[source,star]
----
factorial(zero) => one.
factorial(N) => factorial(N-one)*N.
----

The type signature for this version of `factorial` is given by:

[source,star]
----
factorial: all t ~~ arith[t] |= (t)=>t.
----

which declares that `factorial` is a function over any `t`, provided
that there is an implementation of `arith` for that `t`.

=== Contracts as Interfaces

At their most basic, contracts are a way of denoting `overloaded'
functions. I.e., by giving different implementations of a contract for
different types, it is possible to provide multiple implementations of
a function. For example, there are several implementations of arith
for integers, floats, and so on. It is also straightforward to
implement arith for your own type.

However, contracts are a powerful tool in lifting the basic syntax of the language.

For example, the sequence notation, introduced earlier, is backed by
two contracts: the `stream` contract which is used in sequence
patterns and the `sequence` contract which is used in sequence
expressions.

[source,star]
----
-- stream contract
public contract all S,E ~~ stream[S ->> E] ::= {
  _eof:(S) => boolean.
  _hdtl:(S) => option[(E,S)].
}

public contract all S,E ~~ sequence[S ->> E] ::= {
  _cons:(E,S) => S.
  _nil:S.
}
----

The compiler translates sequence notation to uses of the functions in
these contracts. The result is that sequence expressions are not
_special_. In fact, it is entirely possible for a user-defined type to
be blessed with the sequence notation.

NOTE: The `stream` and `sequence` contracts have a _dependency_:
`E`. This means that the contracts specify a dependency between the
overall sequence type and (in this case) the type of sequence
elements. This also means that we can use sequences over types that
are not generic in nature: for example, it is possible to implement
them for `string` values as well as `cons` lists.

=== Contract Implementations

Contracts are effectively just a type definition. In order to use
them, you need to _implement_ them. For example, the implementation
for `stream` over `cons` lists looks like:

[source,star]
----
public implementation all x ~~ stream[cons[x] ->> x] => {
  _eof(.nil) => .true.
  _eof(.cons(_,_)) => .false.
    
  _hdtl(.cons(H,T)) => .some((H,T)).
  _hdtl(.nil) => .none.
}
----

As a specification of semantics, contracts do not address all the
issues. It is not possible, for example, to express that
implementations of arithmetic obey the normal Peano axioms of
arithmetic.

Many of the syntactic features of Star are actually backed by
contracts. Apart from sequence expressions, accessing collections,
displaying values, coercions between types and support for coroutines
all represent syntactic features of the language whose semantics is
provided via contracts.

== Other Features

This tour is necessarily incomplete. Other notable features of Star include a
built-in grammar notation, exceptions, yield-style generators, async functions,
and actors.

